apply plugin: 'com.android.application'
apply from: 'themes.gradle'

def cDisabled = 0xADADAD
android {

    flavorDimensions "different_appid", "db_impl"

    signingConfigs {
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.0'
    defaultConfig {
        applicationId "pl.selvin.android.listsyncsample"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 9001
        versionName "0.9.1"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    afterEvaluate {
        android.applicationVariants.all { variant ->
            variant.resValue 'string', 'application_id', variant.applicationId
            for(flavor in variant.getProductFlavors()){
                flavor.themes.createTask(variant.name)
            }
        }
    }

    productFlavors {
        prod {
            dimension "different_appid"
            resValue 'string', 'app_name', "List Sync Sample"
        }
        dev {
            dimension "different_appid"
            versionNameSuffix "(dev)"
            applicationIdSuffix ".dev"
            resValue 'string', 'app_name', "List Sync Sample(dev)"
        }
        android{
            dimension "db_impl"
        }
        cipher{
            dimension "db_impl"
            versionNameSuffix "(cipher)"
            applicationIdSuffix ".cipher"
        }

        IndigoPink {
            themes {
                colorsWithAlpha {
                    colorPrimary 0x3F51B5
                    colorPrimaryDark 0x1A237E
                    colorPrimaryLight 0x586ACE
                    colorAccent 0xF50057
                    colorDisabled cDisabled
                }
            }
        }

        GrayBlackWhite {
            themes {
                colorsWithAlpha {
                    colorPrimary 0x3a3a3a
                    colorPrimaryDark 0x111111
                    colorPrimaryLight 0x535353
                    colorAccent 0x78909C
                    colorDisabled cDisabled
                }
            }
        }

        TealLime {
            themes {
                colorsWithAlpha {
                    colorPrimary 0x009688
                    colorPrimaryDark 0x00796B
                    colorPrimaryLight 0x19AFA1
                    colorAccent 0xC6FF00
                    colorDisabled cDisabled
                }
            }
        }
        OrangeBlueGrey {
            themes {
                colorsWithAlpha {
                    colorPrimary 0xF57C00
                    colorPrimaryDark 0xE65100
                    colorPrimaryLight 0xFF9519
                    colorAccent 0x78909C
                    colorDisabled cDisabled
                }
            }
        }
        BrownRed {
            themes {
                colorsWithAlpha {
                    colorPrimary 0x795548
                    colorPrimaryDark 0x5D4037
                    colorPrimaryLight 0xD7CCC8
                    colorAccent 0xFF5252
                    colorDisabled cDisabled
                }
            }
        }
        PurpleGreen {
            themes {
                colorsWithAlpha {
                    colorPrimary 0x9B26AF
                    colorPrimaryDark 0x691A99
                    colorPrimaryLight 0xB43FC8
                    colorAccent 0x68EFAD
                    colorDisabled cDisabled
                }
            }
        }
    }
}

def supportV = '27.0.0'
dependencies {
    implementation "com.android.support:appcompat-v7:$supportV"
    implementation "com.android.support:design:$supportV"
    implementation "com.android.support:recyclerview-v7:$supportV"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.2'
    implementation 'com.google.android:flexbox:0.3.1'
    implementation project(':sync-framework')
    cipherImplementation project(':database-sqlcipher')
    cipherImplementation 'net.zetetic:android-database-sqlcipher:3.5.7@aar'
    androidImplementation project(':database-android')
}
