apply plugin: 'com.android.application'
apply from: 'themes.gradle'

//set to true for sqlcipher support
ext.demo_use_sqlcipher = false

def supportV = '26.0.1'
dependencies {
    compile "com.android.support:appcompat-v7:$supportV"
    compile "com.android.support:design:$supportV"
    compile "com.android.support:recyclerview-v7:$supportV"
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.8'
    compile 'com.google.android:flexbox:0.3.1'
    compile project(':sync-framework')
    if (demo_use_sqlcipher) {
        compile project(':database-sqlcipher')
        compile 'net.zetetic:android-database-sqlcipher:3.5.7@aar'
    } else {
        compile project(':database-android')
    }
}

def colorDisabled = 0xADADAD
android {

    flavorDimensions "different_appid", "theme"

    signingConfigs {
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    defaultConfig {
        applicationId "pl.selvin.android.listsyncsample"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 9001
        versionName "0.9.1"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //it's needed for my easy theming
    productFlavors.whenObjectAdded { flavor ->
        themes_createExtension(flavor)
    }

    afterEvaluate {
        android.applicationVariants.all { variant ->
            variant.resValue 'string', 'application_id', variant.applicationId
        }
    }

    productFlavors {
        prod {
            dimension "different_appid"
            resValue 'string', 'app_name', "List Sync Sample"
        }
        dev {
            dimension "different_appid"
            versionNameSuffix "(dev)"
            applicationIdSuffix ".dev"
            resValue 'string', 'app_name', "List Sync Sample(dev)"
        }

        IndigoPink {
            dimension "theme"
            themes.useTheme 0x3F51B5, 0x1A237E, 0x586ACE, 0xF50057, colorDisabled
        }

        GrayBlackWhite {
            dimension "theme"
            themes.useTheme 0x3a3a3a, 0x111111, 0x535353, 0x78909C, colorDisabled
        }

        TealLime {
            dimension "theme"
            themes.useTheme 0x009688, 0x00796B, 0x19AFA1, 0xC6FF00, colorDisabled
        }
        OrangeBlueGrey {
            dimension "theme"
            themes.useTheme 0xF57C00, 0xE65100, 0xFF9519, 0x78909C, colorDisabled
        }
        BrownRed {
            dimension "theme"
            themes.useTheme 0x795548, 0x5D4037, 0xD7CCC8, 0xFF5252, colorDisabled
        }
        PurpleGreen {
            dimension "theme"
            themes.useTheme 0x9B26AF, 0x691A99, 0xB43FC8, 0x68EFAD, colorDisabled
        }
    }
}
