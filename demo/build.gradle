apply plugin: 'com.android.application'
apply from: 'themes.gradle'
apply from: '../version.gradle'

def disabledColor = 0xADADAD

android {
    flavorDimensions = ["db_impl"]
    buildToolsVersion  = '34.0.0'
    namespace 'pl.selvin.android.listsyncsample'

    defaultConfig {
        compileSdk 34
        applicationId 'pl.selvin.android.listsyncsample'
        minSdkVersion 22
        targetSdkVersion 34
        versionCode sync_framework_version_int
        versionName sync_framework_version
    }

    signingConfigs {
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            versionNameSuffix "(deb)"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
	buildFeatures {
		aidl true
        buildConfig true
	}

	android.applicationVariants.configureEach { variant ->
        variant.resValue 'string', 'application_id', variant.applicationId
    }

    productFlavors {
        android{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample"
            themes {
                data {
                    colorsWithAlpha {
                        colorPrimary 0x3F51B5
                        colorPrimaryDark 0x1A237E
                        colorPrimaryLight 0x586ACE
                        colorAccent 0xF50057
                        colorDisabled disabledColor
                    }
                }
            }
        }

        cipher{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample(cipher)"
            versionNameSuffix "(cipher)"
            applicationIdSuffix ".cipher"
            themes {
                data {
                    colorsWithAlpha {
                        colorPrimary 0x3a3a3a
                        colorPrimaryDark 0x111111
                        colorPrimaryLight 0x535353
                        colorAccent 0x78909C
                        colorDisabled disabledColor
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation project(':sync-framework')
    androidImplementation 'androidx.sqlite:sqlite-framework:2.4.0'
    cipherImplementation "net.zetetic:android-database-sqlcipher:4.5.3"
    cipherImplementation 'com.commonsware.cwac:saferoom.x:1.3.0'
}
