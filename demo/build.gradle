plugins {
    alias libs.plugins.android.gradle.plugin
}
apply from: 'themes.gradle'
apply from: '../version.gradle'

def disabledColor = 0xADADAD

android {
    namespace = 'pl.selvin.android.listsyncsample'
    compileSdk = 35

    flavorDimensions = ["db_impl"]

    defaultConfig {
        applicationId 'pl.selvin.android.listsyncsample'
        minSdkVersion 22
        targetSdkVersion 35
        versionCode sync_framework_version_int
        versionName sync_framework_version
    }

    signingConfigs {
        release {
            keyAlias = 'androiddebugkey'
            keyPassword = 'android'
            storeFile = file('debug.keystore')
            storePassword = 'android'
        }
    }

    buildTypes {
        release {
            shrinkResources = true
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }

        debug {
            versionNameSuffix = "(deb)"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
	buildFeatures {
		aidl = true
        buildConfig = true
	}

	android.applicationVariants.configureEach { variant ->
        variant.resValue 'string', 'application_id', variant.applicationId
    }

    productFlavors {
        android{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample"
            themes {
                data {
                    colorsWithAlpha {
                        colorPrimary 0x3F51B5
                        colorPrimaryDark 0x1A237E
                        colorPrimaryLight 0x586ACE
                        colorAccent 0xF50057
                        colorDisabled disabledColor
                    }
                }
            }
        }

        cipher{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample(cipher)"
            versionNameSuffix "(cipher)"
            applicationIdSuffix ".cipher"
            themes {
                data {
                    colorsWithAlpha {
                        colorPrimary 0x3a3a3a
                        colorPrimaryDark 0x111111
                        colorPrimaryLight 0x535353
                        colorAccent 0x78909C
                        colorDisabled disabledColor
                    }
                }
            }
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.recyclerview
    implementation libs.jsr305
    implementation libs.okhttp3
    implementation libs.flexbox
    implementation libs.constructors.constraints.annotations
    implementation project(':sync-framework')
    androidImplementation libs.androidx.sqlite.framework
    cipherImplementation libs.sqlcipher.android
    annotationProcessor libs.constructors.constraints.processor
}
