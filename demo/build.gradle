apply plugin: 'com.android.application'
apply from: 'themes.gradle'

def cDisabled = 0xADADAD
android {

    flavorDimensions "db_impl"

    signingConfigs {
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "pl.selvin.android.listsyncsample"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode sync_framework_version_int
        versionName sync_framework_version
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "(deb)"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    afterEvaluate {
        android.applicationVariants.all { variant ->
            variant.resValue 'string', 'application_id', variant.applicationId
        }
    }

    productFlavors {
        android{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample"
            themes {
                colorsWithAlpha {
                    colorPrimary 0x3F51B5
                    colorPrimaryDark 0x1A237E
                    colorPrimaryLight 0x586ACE
                    colorAccent 0xF50057
                    colorDisabled cDisabled
                }
            }
        }
        cipher{
            dimension "db_impl"
            resValue 'string', 'app_name', "ListSyncSample(cipher)"
            versionNameSuffix "(cipher)"
            applicationIdSuffix ".cipher"
            themes {
                colorsWithAlpha {
                    colorPrimary 0x3a3a3a
                    colorPrimaryDark 0x111111
                    colorPrimaryLight 0x535353
                    colorAccent 0x78909C
                    colorDisabled cDisabled
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation project(':sync-framework')
    androidImplementation 'androidx.sqlite:sqlite-framework:2.0.0'
    cipherImplementation 'com.commonsware.cwac:saferoom.x:0.5.1'
}
